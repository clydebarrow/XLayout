/*
 * Copyright (c) 2019 Control-J Pty. Ltd. All rights reserved
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 * .
 *
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "digital.wup:android-maven-publish:3.6.2"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}


ext.artifactId = "XLayout"
allprojects {
    group 'com.control-j.xlayout'
    version '1.0.2'

    repositories {
        jcenter()
    }
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "maven-publish"
    apply plugin: "kotlin"
    apply plugin: "signing"
    apply plugin: "org.jetbrains.dokka"

    idea {
        module {
            inheritOutputDirs = false
            outputDir = compileJava.destinationDir
            testOutputDir = compileTestJava.destinationDir
        }
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/dokka"
    }
    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        from dokka
        archiveClassifier = 'javadoc'
    }
    artifacts {
        archives sourcesJar, javadocJar
    }
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    publishing {
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }

        publications {
            mavenJava(MavenPublication) { publication ->
                artifactId = "${-> artifactId}"
                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = 'XLayout core'
                    packaging 'jar'
                    description = 'A code based layout engine'
                    url = 'https://github.com/clydebarrow/XLayout'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'clyde'
                            name = 'Clyde Stubbs'
                            url = 'https://github.com/clydebarrow'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/clydebarrow/XLayout.git'
                        developerConnection = 'scm:git:https://github.com/clydebarrow/XLayout.git'
                        url = 'https://github.com/clydebarrow/XLayout'
                    }
                }
            }
        }
    }
    signing {
        sign configurations.archives
        sign publishing.publications.mavenJava
    }

}

ext {
    kotlinVersion = "1.3.70"
    roboVMVersion = "2.3.7"
}
